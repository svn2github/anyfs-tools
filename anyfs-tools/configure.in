#                                               -*- Autoconf -*-
# Process this file with autoconf to produce a configure script.

AC_PREREQ(2.59)
AC_INIT(anyfs-tools, 0.84.8, undefer@gmail.com)
#AM_INIT_AUTOMAKE(anyfs-tools, 0.84.5c)
AC_CONFIG_SRCDIR([src/reblock.c], [include/any.h])
AC_CONFIG_HEADER([include/config.h])

# Checks for programs.
AC_PROG_CC
AC_PROG_INSTALL
AC_PROG_MAKE_SET

# Checks for packages.
PKG_CONFIG_PATH="$PKG_CONFIG_PATH:/lib/pkgconfig"
export PKG_CONFIG_PATH
EXT2FS_REQUIRED=1.38
PKG_CHECK_MODULES(EXT2FS, ext2fs >= $EXT2FS_REQUIRED )

PKG_CHECK_MODULES(FUSE, fuse >= 2.5, \
		ANYFUSE_MODULE="anyfuse_module"; ANYFUSE="anyfuse", \
		ANYFUSE_MODULE=""; ANYFUSE="" )

if test $ANYFUSE_MODULE; then
	echo -n "checking for fuse kernel module... "
	if LC_ALL=C /sbin/modprobe fuse | grep -q "not found"; then
		echo "yes"
		ANYFUSE_MODULE=""
		ANYFUSE=""
	else
		echo "no"
	fi
fi

echo -n "checking for kernel sources... "
if test -f /lib/modules/`uname -r`/build/Makefile; then
	echo "yes"
	ANYFS_MODULE="anyfs_module"
	ANYFS_KO="anyfs.ko"
else
	echo "no"
	ANYFS_MODULE=""
	ANYFS_KO=""
fi

if test -z "$ANYFS_MODULE" && test -z "$ANYFUSE_MODULE"; then
	echo
	echo "ERROR! Cannot to build anyfs and anyfuse module."
	echo
	echo "You must have fuse library and module to build anyfuse FUSE module"
	echo "Or kernel sources to build anyfs kernel module"
	exit 1
fi

PKG_CHECK_MODULES(MPEG2, libmpeg2, \
		MPEG2_MODULES="audio_video2_files_descr.o"; MPEG2C="", \
		MPEG2_LIBS=""; MPEG2_CFLAGS=""; MPEG2_MODULES=""; MPEG2C="//")

need_lib()
{
	echo "Where your -l$1? I can't work without it!"
	exit 1
}

# Checks for libraries.
AC_CHECK_LIB([dl], [dlsym], , need_lib dl)
AC_CHECK_LIB([e2p], [e2p_os2string], , need_lib e2p)
AC_CHECK_LIB([ext2fs], [ext2fs_read_bb_FILE], , need_lib ext2fs)
AC_CHECK_LIB([uuid], [uuid_generate], , need_lib uuid)
AC_CHECK_LIB([blkid], [blkid_get_devname], , need_lib blkid, , -luuid)
AC_CHECK_LIB([bz2], [BZ2_bzRead],
	     	BZ2LIB="-lbz2"; BZ2MODULES="archieve2_files_descr.o"; BZ2C="", \
		BZ2LIB=""; BZ2MODULES=""; BZ2C="//")

XFS_UTILS=""; BUILD_XFS="";
AC_CHECK_LIB([rt], [aio_init],
AC_CHECK_LIB([disk], [fstype], 
AC_CHECK_LIB([xfs], [libxfs_purgebuf], 
	     XFS_UTILS="build_xfs_util"; BUILD_XFS="build_xfs", , -lrt))) 
	
AC_SUBST(ANYFS_MODULE ANYFS_KO ANYFUSE_MODULE ANYFUSE)
AC_SUBST(EXT2FS_CFLAGS EXT2FS_LIBS)
AC_SUBST(BZ2LIB BZ2MODULES BZ2C)
AC_SUBST(MPEG2_LIBS MPEG2_CFLAGS MPEG2_MODULES MPEG2C)
AC_SUBST(XFS_UTILS BUILD_XFS)

DESTDIR=
AC_SUBST(DESTDIR)

# Checks for header files.
AC_HEADER_DIRENT
AC_HEADER_STDC
AC_HEADER_SYS_WAIT
AC_CHECK_HEADERS([fcntl.h stdint.h stdlib.h string.h sys/ioctl.h sys/vfs.h unistd.h])
AC_CHECK_HEADERS([libintl.h locale.h], LANGS="ru", LANGS="")
AC_SUBST(LANGS)

# Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_UID_T
AC_C_INLINE
AC_TYPE_MODE_T
AC_TYPE_OFF_T
AC_TYPE_PID_T
AC_TYPE_SIZE_T
AC_CHECK_MEMBERS([struct stat.st_blksize])
AC_STRUCT_ST_BLOCKS
AC_CHECK_MEMBERS([struct stat.st_rdev])
AC_HEADER_TIME

# Checks for library functions.
AC_FUNC_CLOSEDIR_VOID
AC_FUNC_ERROR_AT_LINE
AC_FUNC_FORK
AC_PROG_GCC_TRADITIONAL
AC_HEADER_MAJOR
AC_FUNC_MALLOC
AC_FUNC_MEMCMP
AC_FUNC_REALLOC
AC_FUNC_SELECT_ARGTYPES
AC_FUNC_SETVBUF_REVERSED
AC_FUNC_STAT
AC_CHECK_FUNCS([__fpending bzero dup2 fdatasync memmove memset putenv select setlocale strcasecmp strchr strcspn strdup strerror strrchr strspn strstr strtol strtoul strtoull uname])

#AC_CONFIG_FILES([Makefile
#                 anyfs/Makefile
#                 doc/Makefile
#                 doc/man/Makefile
#                 doc/man/man5/Makefile
#                 doc/man/man8/Makefile
#                 doc/man/ru/Makefile
#                 doc/man/ru/man5/Makefile
#                 doc/man/ru/man8/Makefile
#                 src/Makefile
#                 src/anysurrect/Makefile])

AC_OUTPUT(Makefile 
	src/anyfs/Makefile 
	src/anyfuse/Makefile 
	doc/man/man3/Makefile
	doc/man/man5/Makefile
	doc/man/man8/Makefile
	doc/man/ru/man3/Makefile
	doc/man/ru/man5/Makefile
	doc/man/ru/man8/Makefile
	src/Makefile 
	src/anysurrect/Makefile 
	src/anysurrect/surrectlist.conf
	src/anysurrect/gzip/Makefile 
	src/anysurrect/scan_pes/Makefile
	src/build_e2fs/Makefile
	src/build_xfs/Makefile
	include/Makefile
	lib/Makefile
	scripts/Makefile
	po/Makefile
	include/somepaths.h)

if test -z "$MPEG2_LIBS" || test -z "$BZ2LIB" || test -z "$BUILD_XFS" || test -z "$ANYFS_MODULE" || test -z "$ANYFUSE_MODULE"; then
	echo
	echo "WARNING! The package configured successful, but the next features was disabled:"
	echo
	test -z "$ANYFS_MODULE" &&
		echo "* Building anyfs kernel module (depends on kernel sources)"
	test -z "$ANYFUSE_MODULE" &&
		echo "* Building anyfuse FUSE module (depends on fuse library >= 2.5.0, and fuse kernel module)"
	test -z "$MPEG2_LIBS" &&
		echo "* Mpeg2 recovering with anysurrect (depends on libmpeg2)"
	test -z "$BZ2LIB" &&
		echo "* BZIP2 recovering with anysurrect (depends on libbz2)"
	test -z "$BUILD_XFS" &&
		echo "* Building XFS filesystem (depends on xfsprogs>=2.8.10, librt)"
fi

true
